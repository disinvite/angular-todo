Class Todo.REST Extends %CSP.REST
{

XData UrlMap [ XMLNamespace = "http://www.intersystems.com/urlmap" ]
{
<Routes>
<Route Url="/tasks" Method="GET" Call="GetTasks" />
<Route Url="/tasks" Method="POST" Call="NewTask" />
<Route Url="/task/:id" Method="GET" Call="GetTask" />
<Route Url="/task/:id" Method="PUT" Call="UpdateTask" />
<Route Url="/task/:id" Method="DELETE" Call="DeleteTask" />
</Routes>
}

ClassMethod GetTasks() As %Status
{
	Set return = {"tasks": []}
	Set rs = ##class(%SQL.Statement).%ExecDirect(,"SELECT id,text,important,completed FROM Todo.Task")
	While rs.%Next() {
		Do return.tasks.%Push({
			"id":        (rs.%Get("id")),
			"text":      (rs.%Get("text")),
			"important": (rs.%Get("important")),
			"completed": (rs.%Get("completed"))
		})
	}
	Write return.%ToJSON()
	Quit 1
}

ClassMethod GetTask(id As %Integer) As %Status
{
	Set task = ##class(Todo.Task).%OpenId(id)
	If '$IsObject(task) {
		Set %response.Status = ..#HTTP404NOTFOUND
		Quit 1
	}
	
	Set obj = {
		"id":        (task.%Id()),
		"text":      (task.text),
		"important": (task.important),
		"completed": (task.completed)
	}
	
	Write obj.%ToJSON()
	
	Quit 1
}

ClassMethod NewTask() As %Status
{
	Set data = ##class(%DynamicObject).%FromJSON(%request.Content)
	Set task = ##class(Todo.Task).%New()
	Set task.text = data.text
	
	If data.%IsDefined("important") {
		Set task.important = data.important
	} Else {
		Set task.important = 0
	}
	Set task.completed = 0
	Do task.%Save()
	
	Quit 1
}

ClassMethod UpdateTask(id As %Integer) As %Status
{
	Set data = ##class(%DynamicObject).%FromJSON(%request.Content)
	
	Set task = ##class(Todo.Task).%OpenId(id)
	If '$IsObject(task) {
		Set %response.Status = ..#HTTP404NOTFOUND
		Quit 1
	}
	
	If data.%IsDefined("text") {
		Set task.text = data.text
	}
	
	If data.%IsDefined("important") {
		Set task.important = data.important
	}
	
	If data.%IsDefined("completed") {
		Set task.completed = data.completed
	}
	
	Do task.%Save()
	Quit 1
}

ClassMethod DeleteTask(id As %Integer) As %Status
{
	If '##class(Todo.Task).%ExistsId(id) {
		Set %response.Status = ..#HTTP404NOTFOUND
		Quit 1
	}
	Do ##class(Todo.Task).%DeleteId(id)
	
	Quit 1
}

}
